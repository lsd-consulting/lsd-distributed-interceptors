sourceSets {
    integrationMongoTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationMongoTest/java')
        }
    }
}

configurations {
    integrationMongoTestImplementation.extendsFrom implementation
}

dependencies {

    // Spring
    integrationMongoTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationMongoTestImplementation 'org.springframework.boot:spring-boot-starter-web'
    integrationMongoTestImplementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    integrationMongoTestImplementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    integrationMongoTestImplementation 'io.micrometer:micrometer-tracing'
    integrationMongoTestImplementation 'io.micrometer:micrometer-tracing-bridge-brave'
    integrationMongoTestImplementation 'io.micrometer:micrometer-tracing-test'

    // Other
    integrationMongoTestImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.5.4'
    integrationMongoTestImplementation 'org.junit.platform:junit-platform-launcher'
    integrationMongoTestImplementation 'com.github.fridujo:rabbitmq-mock:1.1.1'
    integrationMongoTestImplementation 'org.awaitility:awaitility'
    integrationMongoTestImplementation 'org.wiremock:wiremock-standalone:3.12.1'
    integrationMongoTestImplementation 'io.github.k-random:k-random-core:1.29.3'

    // LSD
    integrationMongoTestImplementation 'io.github.lsd-consulting:lsd-distributed-mongodb-connector:6.1.8'
    integrationMongoTestImplementation 'io.github.lsd-consulting:lsd-junit5:7.0.117'
    integrationMongoTestImplementation 'io.github.lsd-consulting:lsd-distributed-generator:8.2.34'
}

tasks.register('integrationMongoTest', Test) {
    useJUnitPlatform()
    dependsOn assemble

    testClassesDirs = sourceSets.integrationMongoTest.output.classesDirs
    classpath = sourceSets.integrationMongoTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging.showStandardStreams = false
    mustRunAfter(test)
    finalizedBy jacocoTestReport
}

check.dependsOn(integrationMongoTest)

integrationMongoTest {
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Mongo integration test results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}
