import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.palantir.git-version' version '0.12.3'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.7.0'
}

group = 'io.github.lsd-consulting'
version = gitVersion().replaceAll("^v", "")
println "Build Version = ${version}"

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenLocal()
    mavenCentral()
}

jacocoTestReport {
    reports {
        xml.enabled(true)
        html.enabled(true)
        html.setDestination(project.provider(() -> new File("${project.buildDir}/reports/coverage")))
    }
    executionData(
            file("${project.buildDir}/jacoco/test.exec"),
            file("${project.buildDir}/jacoco/integrationTest.exec")
    )
}

apply plugin: 'io.spring.dependency-management'
dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2020.0.3'
        mavenBom 'io.pivotal.spring.cloud:spring-cloud-services-dependencies:3.2.0.RELEASE'
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

apply from: 'dependencies.gradle'
apply from: 'publishing.gradle'
apply from: 'test.gradle'
apply from: 'integrationTest.gradle'
apply from: 'pitest.gradle'
apply from: 'hooks.gradle'
