sourceSets {
    springIntegrationHttpTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/springIntegrationHttpTest/java')
        }
    }
}

configurations {
    springIntegrationHttpTestImplementation.extendsFrom implementation
}

dependencies {

    // Spring
    springIntegrationHttpTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    springIntegrationHttpTestImplementation 'io.micrometer:micrometer-tracing'
    springIntegrationHttpTestImplementation 'io.micrometer:micrometer-tracing-bridge-brave'
    springIntegrationHttpTestImplementation 'io.micrometer:micrometer-tracing-test'
    springIntegrationHttpTestImplementation 'org.springframework.boot:spring-boot-starter-web'
    springIntegrationHttpTestImplementation 'org.springframework.kafka:spring-kafka'
    springIntegrationHttpTestImplementation 'org.springframework.kafka:spring-kafka-test'

    // Other
    springIntegrationHttpTestImplementation 'org.awaitility:awaitility'
    springIntegrationHttpTestImplementation 'org.wiremock:wiremock-standalone:3.12.1'
    springIntegrationHttpTestImplementation 'io.github.k-random:k-random-core:1.29.3'

    // LSD
    springIntegrationHttpTestImplementation 'io.github.lsd-consulting:lsd-distributed-http-connector:4.0.42'
    springIntegrationHttpTestImplementation('io.github.lsd-consulting:lsd-distributed-generator-ui-service:7.2.0:wiremock-stubs'){
        exclude group: "*", module: "*"
    }

    springIntegrationHttpTestImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}

tasks.register('springIntegrationHttpTest', Test) {
    useJUnitPlatform()
    dependsOn assemble

    testClassesDirs = sourceSets.springIntegrationHttpTest.output.classesDirs
    classpath = sourceSets.springIntegrationHttpTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging.showStandardStreams = false
    mustRunAfter(integrationHttpTest)
    finalizedBy jacocoTestReport
}

check.dependsOn(springIntegrationHttpTest)

springIntegrationHttpTest {
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Http integration test results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}
