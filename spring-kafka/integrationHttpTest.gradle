sourceSets {
    integrationHttpTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationHttpTest/java')
        }
    }
}

configurations {
    integrationHttpTestImplementation.extendsFrom implementation
}

dependencies {

    // Spring
    integrationHttpTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationHttpTestImplementation 'io.micrometer:micrometer-tracing-bridge-brave'
    integrationHttpTestImplementation 'io.micrometer:micrometer-tracing-test'
    integrationHttpTestImplementation 'org.springframework.boot:spring-boot-starter-web'
    integrationHttpTestImplementation 'org.springframework.kafka:spring-kafka'
    integrationHttpTestImplementation 'org.springframework.kafka:spring-kafka-test'

    // Other
    integrationHttpTestImplementation 'org.awaitility:awaitility'
    integrationHttpTestImplementation 'org.wiremock:wiremock-standalone:3.12.1'
    integrationHttpTestImplementation 'io.github.k-random:k-random-core:1.29.3'

    // LSD
    integrationHttpTestImplementation 'io.github.lsd-consulting:lsd-distributed-http-connector:4.0.42'
    integrationHttpTestImplementation('io.github.lsd-consulting:lsd-distributed-generator-ui-service:7.2.0:wiremock-stubs'){
        exclude group: "*", module: "*"
    }

    integrationHttpTestImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}

tasks.register('integrationHttpTest', Test) {
    useJUnitPlatform()
    dependsOn assemble

    testClassesDirs = sourceSets.integrationHttpTest.output.classesDirs
    classpath = sourceSets.integrationHttpTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging.showStandardStreams = false
    mustRunAfter(test)
    finalizedBy jacocoTestReport
}

check.dependsOn(integrationHttpTest)

integrationHttpTest {
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Http integration test results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}
